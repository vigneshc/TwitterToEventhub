--Add timestamp by expression, convert tweet text into distinct words array with javascript udf, project user mentions array and hashtags array
WITH Tweets1 AS 
(
    SELECT
        PartitionId,
        [id],
        DATEADD(ms, CAST([InputEventhub].timestamp_ms as BIGINT), '01-01-1970') as [TweetCreatedTime],
        udf.GetDistinctWordsArray([InputEventhub].text ) [UniqueWords],
        udf.AddItemToArray([InputEventhub].entities.hashtags, '{"text":"#global#"}') [HashTagsArray],
        udf.AddItemToArray([InputEventhub].entities.user_mentions, '{"screen_name":"#global#"}') [UserMentionsArray]
    FROM 
        [InputEventhub] PARTITION BY PartitionId
    TIMESTAMP BY 
        DATEADD(ms, CAST([InputEventhub].timestamp_ms as BIGINT), '01-01-1970')
    WHERE
        lang = 'en'
),

--cross apply with three arrays - user mentions, hash tags and unique words. Creates multiple rows per record.
[TweetsAndEntities] AS
(
SELECT
    [Tweets1].[PartitionId],
    [Tweets1].[TweetCreatedTime],
    System.Timestamp as [SystemAssignedTime],
    [Tweets1].[id] as [TweetId],
    hashtags.ArrayValue.text as [HashTag],
    userMentions.ArrayValue.screen_name as [MentionedTwitterHandle],
    tweetWord.ArrayValue as [word]
FROM
    [Tweets1] PARTITION BY PartitionId
    CROSS APPLY GetArrayElements([Tweets1].[HashTagsArray]) as hashtags
    CROSS APPLY GetArrayElements([Tweets1].[UserMentionsArray]) as userMentions
    CROSS APPLY GetArrayElements([Tweets1].[UniqueWords]) as tweetWord
),

--Tweet count for a (#hashtag, @usermention) combination per partition.
[HashTagUserMentionPartitionWindowCounts] AS
(
SELECT
    [PartitionId],
    System.Timestamp [WindowTime],
    [HashTag],
    [MentionedTwitterHandle],
    COUNT(DISTINCT TWEETID) [TweetCount]
FROM
    [TweetsAndEntities] PARTITION BY PartitionId
GROUP BY
    [PartitionId],
    [HashTag],
    [MentionedTwitterHandle],
    TUMBLINGWINDOW( mi, 15)
),

--Global tweet count  (#hashtag, @usermention) combination. Possible because each tweet is sent only to one partition.
[HashTagUserMentionGlobalWindowCountsQuery] AS
(
SELECT
    System.Timestamp [WindowTime],
    [HashTag],
    [MentionedTwitterHandle],
    Sum([TweetCount]) [TweetCount],
    3 [QueryVersion]
FROM
    [HashTagUserMentionPartitionWindowCounts]
GROUP BY
    [HashTag],
    [MentionedTwitterHandle],
    TUMBLINGWINDOW( mi, 15)
),
-- Tweet count for (Word, #HashTag) pair per partition.
[HashTagWordPartitionWindowCounts] AS
(
SELECT
    [PartitionId],
    System.Timestamp [WindowTime],
    [HashTag],
    [word],
    COUNT(DISTINCT TWEETID) [TweetCount]
FROM
    [TweetsAndEntities] PARTITION BY PartitionId
GROUP BY
    [PartitionId],
    [HashTag],
    [word],
    TUMBLINGWINDOW( mi, 15)
),
-- Global Tweet count for (Word, #HashTag) 
[HashTagWordGlobalWindowCountsQuery] AS
(
SELECT
    System.Timestamp [WindowTime],
    [HashTag],
    [word],
    Sum([TweetCount]) [TweetCount],
    3 [QueryVersion]
FROM
    [HashTagWordPartitionWindowCounts]
GROUP BY
    [HashTag],
    [word],
    TUMBLINGWINDOW( mi, 15)
),

-- (UserMention, Word) pair tweet count per partition.
[UserMentionWordPartitionWindowCounts] AS
(
SELECT
    [PartitionId],
    System.Timestamp [WindowTime],
    [MentionedTwitterHandle],
    [word],
    COUNT(DISTINCT TWEETID) [TweetCount]
FROM
    [TweetsAndEntities] PARTITION BY PartitionId
GROUP BY
    [PartitionId],
    [MentionedTwitterHandle],
    [word],
    TUMBLINGWINDOW( mi, 15)
),

-- Global (UserMention, Word) pair tweet count.
[UserMentionWordGlobalWindowCountsQuery] AS
(
SELECT
    System.Timestamp [WindowTime],
    [MentionedTwitterHandle],
    [word],
    Sum([TweetCount]) [TweetCount],
    3 [QueryVersion]
FROM
    [UserMentionWordPartitionWindowCounts]
GROUP BY
    [MentionedTwitterHandle],
    [word],
    TUMBLINGWINDOW( mi, 15)
),
--Get top 10 hashtags by tweetcount.
[GlobalTopHashTagCountsStep1] AS
(
SELECT
    System.Timestamp [WindowTime],
    CollectTop(10) OVER (ORDER BY TweetCount DESC) as Top10 --Get top 10 hashtags by tweetcount
FROM
    [HashTagUserMentionGlobalWindowCountsQuery]
WHERE
    [MentionedTwitterHandle] = '#global#' --this query only cares about hashtag, select one row per hashtag.
    and [HashTag] <> '#global#' --ignore the dummy row.
GROUP BY
    TUMBLINGWINDOW( mi, 15)
),
--Flatten out CollectTop output.
[GlobalTopHashTagCounts] AS
(
    SELECT
        [GlobalTopHashTagCountsStep1].[WindowTime],
        Top10Elements.ArrayValue.value.hashtag [HashTag],
        Top10Elements.ArrayValue.value.tweetcount [TweetCount]
    FROM
        [GlobalTopHashTagCountsStep1]
    CROSS APPLY GetArrayElements([GlobalTopHashTagCountsStep1].Top10) as Top10Elements
),
--Do a self join and select hashtags that are no longer mentioned.
[PopularHashTagsThatAreNoLongerMentionedQuery] AS
(
SELECT
    System.Timestamp [JoinTimeStamp],
    [Old].[WindowTime] [OldWindowTime],
    [Old].[HashTag],
    [Old].[TweetCount],
    3 [QueryVersion]
FROM
    [GlobalTopHashTagCounts] as [Old]
LEFT JOIN
    [GlobalTopHashTagCounts] as [New]
ON
    [Old].[HashTag] = [New].[HashTag]
    AND DATEDIFF(minute, [Old], [New]) BETWEEN 0 AND 15
WHERE
    [New].HashTag is null
)


SELECT * INTO [UserMentionWordGlobalWindowCounts] FROM UserMentionWordGlobalWindowCountsQuery
SELECT * INTO [HashTagWordGlobalWindowCounts] FROM HashTagWordGlobalWindowCountsQuery
SELECT * INTO [HashTagUserMentionGlobalWindowCounts] FROM HashTagUserMentionGlobalWindowCountsQuery
SELECT * INTO [PopularHashTagsThatAreNoLongerMentioned] FROM PopularHashTagsThatAreNoLongerMentionedQuery

